#!/usr/bin/awk -f
# ---. *
# |__| , ,--. ---. ,--.
# |    | ,--| |  | |  |
# '    ' `--` '  ' `--'                                               2016-10-08
#-------------------------------------------------------------------------------
# Console speaker-test piano

BEGIN {

	# qwerty
#  ____ ____ ____ ____ ____ ____ ____ ____ ____ ____ ____ ____ ____ ______
# |Esc |F1  |F2  |F3  |F4  |F5  |F6  |F7  |F8  |F9  |F10 |F11 |F12 |Del   |
# |____|____|____|____|____|____|____|____|____|____|____|____|____|______|
# |` ~ |1 ! |2 @ |3 # |4 $ |5 % |6 ^ |7 & |8 * |9 ( |0 ) |- _ |= + |Bkspc |
# |____|____|____|____|____|____|____|____|____|____|____|____|____|______|
# |Tab  |Q   |W   |E   |R   |T   |Y   |U   |I   |O   |P   |[ { |] } |     |
# |_____|____|____|____|____|____|____|____|____|____|____|____|____| <-' |
# |Caps  |A   |S   |D   |F   |G   |H   |J   |K   |L   |; : |' " |\ | |    |
# |______|____|____|____|____|____|____|____|____|____|____|____|____|____|
# |Shift  |Z   |X   |C   |V   |B   |N   |M   |, < |. > |/ ? |Shift        |
# |_______|____|____|____|____|____|____|____|____|____|____|_____________|
# |Ctrl  |Super |Alt   |Space                          |Alt Gr|Opt  |Ctrl |
# |______|______|______|_______________________________|______|_____|_____|

	#        Bottom two rows      Top two rows
	qwerty = "zsxdcvgbhnjm,l.;/"  "q2w3er5t6y7ui9o0p[=]"; # Without shift
	QWERTY = "ZSXDCVGBHNJM<L>:/"  "Q@W#ER%T^Y&UI(O)P{+}"; # With    shift
	qwerty = qwerty QWERTY;

	# dvorak
#  ____ ____ ____ ____ ____ ____ ____ ____ ____ ____ ____ ____ ____ ______
# |Esc |F1  |F2  |F3  |F4  |F5  |F6  |F7  |F8  |F9  |F10 |F11 |F12 |Del   |
# |____|____|____|____|____|____|____|____|____|____|____|____|____|______|
# |` ~ |1 ! |2 @ |3 # |4 $ |5 % |6 ^ |7 & |8 * |9 ( |0 ) |[ { |] } |Bkspc |
# |____|____|____|____|____|____|____|____|____|____|____|____|____|______|
# |Tab  |' " |, < |. > |P   |Y   |F   |G   |C   |R   |L   |/ ? |= + |     |
# |_____|____|____|____|____|____|____|____|____|____|____|____|____| <-' |
# |Caps  |A   |O   |E   |U   |I   |D   |H   |T   |N   |S   |- _ |\ | |    |
# |______|____|____|____|____|____|____|____|____|____|____|____|____|____|
# |Shift  |; : |Q   |J   |K   |X   |B   |M   |W   |V   |Z   |Shift        |
# |_______|____|____|____|____|____|____|____|____|____|____|_____________|
# |Ctrl  |Super |Alt   |Space                          |Alt Gr|Opt  |Ctrl |
# |______|______|______|_______________________________|______|_____|_____|

	#        Bottom two rows      Top two rows
	dvorak = ";oqejkixdbhmwnvsz"  "'2,3.p5y6f7gc9r0l/]="; # Without shift
	DVORAK = ":OQEJKIXDBHMWNVSZ" "\"@<#>P%Y^F&GC(R)L?}+"; # With    shift
	dvorak = dvorak DVORAK;

	layout = dvorak;

	# notes frequencies

	C0  = 16   " "; Cd0 = 17   " "; D0  = 18   " "; Dd0 = 19   " ";
	E0  = 21   " "; F0  = 22   " "; Fd0 = 23   " "; G0  = 25   " ";
	Gd0 = 26   " "; A0  = 28   " "; Ad0 = 29   " "; B0  = 31   " ";
	C1  = 33   " "; Cd1 = 35   " "; D1  = 37   " "; Dd1 = 39   " ";
	E1  = 41   " "; F1  = 44   " "; Fd1 = 46   " "; G1  = 49   " ";
	Gd1 = 52   " "; A1  = 55   " "; Ad1 = 58   " "; B1  = 62   " ";
	C2  = 65   " "; Cd2 = 69   " "; D2  = 73   " "; Dd2 = 78   " ";
	E2  = 82   " "; F2  = 87   " "; Fd2 = 93   " "; G2  = 98   " ";
	Gd2 = 104  " "; A2  = 110  " "; Ad2 = 117  " "; B2  = 123  " ";
	C3  = 131  " "; Cd3 = 139  " "; D3  = 147  " "; Dd3 = 156  " ";
	E3  = 165  " "; F3  = 175  " "; Fd3 = 185  " "; G3  = 196  " ";
	Gd3 = 208  " "; A3  = 220  " "; Ad3 = 233  " "; B3  = 247  " ";
	C4  = 262  " "; Cd4 = 277  " "; D4  = 294  " "; Dd4 = 311  " ";
	E4  = 330  " "; F4  = 349  " "; Fd4 = 370  " "; G4  = 392  " ";
	Gd4 = 415  " "; A4  = 440  " "; Ad4 = 466  " "; B4  = 494  " ";
	C5  = 523  " "; Cd5 = 554  " "; D5  = 587  " "; Dd5 = 622  " ";
	E5  = 659  " "; F5  = 698  " "; Fd5 = 740  " "; G5  = 784  " ";
	Gd5 = 831  " "; A5  = 880  " "; Ad5 = 932  " "; B5  = 988  " ";
	C6  = 1047 " "; Cd6 = 1109 " "; D6  = 1175 " "; Dd6 = 1245 " ";
	E6  = 1319 " "; F6  = 1397 " "; Fd6 = 1480 " "; G6  = 1568 " ";
	Gd6 = 1661 " "; A6  = 1760 " "; Ad6 = 1865 " "; B6  = 1976 " ";
	C7  = 2093 " "; Cd7 = 2217 " "; D7  = 2349 " "; Dd7 = 2489 " ";
	E7  = 2637 " "; F7  = 2794 " "; Fd7 = 2960 " "; G7  = 3136 " ";
	Gd7 = 3322 " "; A7  = 3520 " "; Ad7 = 3729 " "; B7  = 3951 " ";
	C8  = 4186 " "; Cd8 = 4435 " "; D8  = 4699 " "; Dd8 = 4978 " ";
	E8  = 5274 " "; F8  = 5588 " "; Fd8 = 5920 " "; G8  = 6272 " ";
	Gd8 = 6645 " "; A8  = 7040 " "; Ad8 = 7459 " "; B8  = 7902 " ";

	# notes selection

	print "\
 _________________________________________________________________________\n\
 |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  |  |\n\
 |  |Db|  |Eb|  |  |Gb|  |Ab|  |Bb|  |  |Db|  |Eb|  |  |Gb|  |Ab|  |Bb|  |\n\
 |  |C#|  |D#|  |  |F#|  |G#|  |A#|  |  |C#|  |D#|  |  |F#|  |G#|  |A#|  |\n\
 |  |__|  |__|  |  |__|  |__|  |__|  |  |__|  |__|  |  |__|  |__|  |__|  |\n\
 |    |    |    |    |     |    |    |    |    |    |    |     |    |    |\n\
 | C  | D  | E  | F  | G   | A  | B  | C  | D  | E  | F  | G   | A  | B  |\n\
 |____|____|____|____|_____|____|____|____|____|____|____|_____|____|____|\n\
\n\
  ____ ____ ____ ____ ____ ____ ____ ____ ____ ____ ____ ____ ____ ______ \n\
 |Esc |F1  |F2  |F3  |F4  |F5  |F6  |F7  |F8  |F9  |F10 |F11 |F12 |Del   |\n\
 |____|____|____|____|____|____|____|____|____|____|____|____|____|______|\n\
 |    |    |C#Eb|D#Eb|    |F#Gb|G#Ab|A#Bb|    |C#Db|D#Ab|    |F#Gb|Bkspc |\n\
 |____|____|____|____|____|____|____|____|____|____|____|____|____|______|\n\
 |Tab  |C   |D   |E   |F   |G   |A   |B   |C   |D   |E   |F   |G   |     |\n\
 |_____|____|____|____|____|____|____|____|____|____|____|____|____| <-' |\n\
 |Caps  |    |C#Eb|D#Eb|    |F#Gb|G#Ab|A#Bb|    |C#Db|D#Eb|    |    |    |\n\
 |______|____|____|____|____|____|____|____|____|____|____|____|____|____|\n\
 |Shift  |C   |D   |E   |F   |G   |A   |B   |C   |D   |E   |Shift        |\n\
 |_______|____|____|____|____|____|____|____|____|____|____|_____________|\n\
 |Ctrl  |Super |Alt   |Space                          |Alt Gr|Opt  |Ctrl |\n\
 |______|______|______|_______________________________|______|_____|_____|\n"


	row1 = C3 Cd3 D3 Dd3 E3 F3 Fd3 G3 Gd3 A3 Ad3 B3 C4 Cd4 D4 Dd4 E4;
	row2 = C4 Cd4 D4 Dd4 E4 F4 Fd4 G4 Gd4 A4 Ad4 B4 C5 Cd5 D5 Dd5 E5 F5 Fd5 G5;

	# algorythm

	split(row1 row2, frequencies, " ");

	while (1) {
		cmd = "dd bs=1 count=1 if=/dev/tty 2>/dev/null"
		system("stty raw -echo");
		cmd | getline key
		system("stty -raw echo");
		close(cmd);

		if (key == "") {
			exit;
		} else if (note = frequencies[index(layout, key)]) {
			system("speaker-test -t sine -f " note "& pid=$! && sleep 0.5 && kill -9 $pid &");
		}
	}
}
