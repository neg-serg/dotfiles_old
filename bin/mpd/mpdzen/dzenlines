#!/bin/zsh

zmodload zsh/datetime

integer offset=0

# parse those parameters into an associate array
typeset -A pars
# alternating key value pairs will do the right thing here
pars=( "$@" )

# set some default parameters, if they are not there
[[ -z $pars[-h] ]] && pars[-h]=20
[[ -z $pars[-p] ]] && pars[-p]=3
[[ -z $pars[-y] ]] && pars[-y]=0

# make sure this dir exists
[[ ! -d /tmp/dzenlines ]] && mkdir /tmp/dzenlines

# this is not threadsafe, but w/e
fname=/tmp/dzenlines/y_$pars[-y]_x_${${pars[-x]}:-0}
[[ -e $fname ]] && l=( ${(f)"$(<$fname)"} )
[[ -n $l ]] && (( $l[1] > $EPOCHSECONDS )) && offset=$[ $l[2] +4 ]
echo "$(($EPOCHSECONDS+$pars[-p]))\n$(($offset+$pars[-h]))" > $fname

# adjust y parameter
if (( $pars[-y] > 0 )); then
    pars[-y]=$[ ${pars[-y]} + $offset ]
else
    pars[-y]=$[ ${pars[-y]} - $offset ]
fi

# forward modified parameters to dzen2
dzen2 ${(kv)pars}
