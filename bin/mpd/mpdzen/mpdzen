#!/bin/zsh

RESPATH=( $0(:A:h) )

FN_ARTIST='-*-*-*-*-*-*-18-*-*-*-*-*-*-*'
CL_ARTIST='#5A863A'
FN_SONG='-*-*-*-*-*-*-14-*-*-*-*-*-*-*'
CL_SONG='#BDBABD'
FN_ALBUM='-*-*-*-*-*-*-10-*-*-*-*-*-*-*'
CL_ALBUM='#BDBABD'

FN_TIME='-*-*-*-*-*-*-8-*-*-*-*-*-*-*'
CL_TIME='#425142'

FN_INFO='-*-*-*-*-*-*-8-*-*-*-*-*-*-*'
CL_INFO=''

[[ -n $commands[dzenlines] ]] && dzen=$commands[dzenlines] || dzen=dzen2

# get some params
self=( $0(:A:h) )
[[ -e $self/mpc_parse ]] || { echo 'Missing mpc_parse!'; return 1 }
# note mpc_parse uses $1 as hostname
. ${self}/mpc_parse

# make sure the parsing succeeded - we have no proper data otherwise
[[ $? != 0 ]] && return 1

AW=$(textwidth "$FN_ARTIST" "$ARTIST")
while [[ $AW -gt 292 ]]; do
    ARTIST=$ARTIST[1,-4]'..'
    AW=$(textwidth "$FN_ARTIST" "$ARTIST")
done

SW=$(textwidth "$FN_SONG" "$SONG")
while [[ $SW -gt 310 ]]; do
    SONG=$SONG[1,-4]'..'
    SW=$(textwidth "$FN_SONG" "$SONG")
done

W=360

# dynamic width
fixw=90
(( $AW+$fixw > $W )) && W=$[ $fixw + $AW ]
(( $SW+$fixw > $W )) && W=$[ $fixw + $SW ]

# any cover here? match case-insensitively and allow two errors in the name.
setopt extended_glob
img=( covers/(#ia3)${~ARTIST}/${~${(q)ALBUM}:gs/\//_}.xpm(N) )
[[ -z $img ]] && img=( ~/.cache/covers/(#ia3)${~ARTIST}/${~${ALBUM}:gs/\//_}.xpm(N) )
if [[ -n $img ]]; then
    img=${${img[1]}:gs/)/\\)}
    img="^i($img)"
else
    img="^fg(#324132)^r(64x64)^pa(36;28)^fg(#000000)?"
fi

case $MPDSTATE in
    playing) STATE="^fg(#ffffff)^i($RESPATH/play.xbm) " ;;
     paused) STATE="^fg(#ffffff)^i($RESPATH/pause.xbm) " ;;
esac

# long interpret or song title? switch layout then.
if (( $AW > 220 || $SW > 220)); then
    (( $W < 395 )) && W=395
    # this one moves 
    echo "^ib(1)" \
    "^pa(8;4)$img" \
    "^pa(80;8)$STATE" \
    "^pa(98;4)^fg($CL_ARTIST)^fn($FN_ARTIST)$ARTIST" \
    "^pa(80;28)^fg($CL_SONG)^fn($FN_SONG)$SONG" \
    "^pa(80;49)^fg(#324132)^r(200x6)^pa(80;49)^fg($CL_TIME)^r($(($PERCENT*2))x6)" \
    "^pa(300;48)^fg($CL_TIME)^fn($FN_TIME)$TIME_NOW / $TIME_TOTAL" \
    "^pa(365;54)^fg($CL_INFO)^fn($FN_INFO)$PL_NOW / $PL_TOTAL" \
    "^pa(80;58)^fg($CL_INFO)^fn($FN_INFO)$STATUS" | $dzen -p 3 -h 72 -x $[ 1275-$W ] -y 20 -w $W -ta l
else
    echo "^ib(1)" \
    "^pa(8;4)$img" \
    "^pa(80;8)$STATE" \
    "^pa(98;4)^fg($CL_ARTIST)^fn($FN_ARTIST)$ARTIST" \
    "^pa($(($W-30));14)^fg($CL_INFO)^fn($FN_INFO)$PL_NOW / $PL_TOTAL" \
    "^pa(80;28)^fg($CL_SONG)^fn($FN_SONG)$SONG" \
    "^pa(80;49)^fg(#324132)^r(200x6)^pa(80;49)^fg($CL_TIME)^r($(($PERCENT*2))x6)" \
    "^pa(300;48)^fg($CL_TIME)^fn($FN_TIME)$TIME_NOW / $TIME_TOTAL" \
    "^pa(80;58)^fg($CL_INFO)^fn($FN_INFO)$STATUS" | $dzen -p 3 -h 72 -x $[ 1275-$W ] -y 20 -w $W -ta l
fi

# couldn't fit this in the layout..
# "^pa(260;12)^fg($CL_ALBUM)^fn($FN_ALBUM)$ALBUM"

